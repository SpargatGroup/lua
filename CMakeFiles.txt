cmake_minimum_required(VERSION 3.16)
project(lua
    VERSION 5.5
    LANGUAGES C
)
option(LUA_BUILD_SHARED "Build Lua as shared library" OFF)
file(GLOB LUA_SOURCES
    CONFIGURE_DEPENDS
    ${CMAKE_CURRENT_SOURCE_DIR}/src/*.c
)
list(FILTER LUA_SOURCES EXCLUDE REGEX ".*/lua\\.c$")
if (LUA_BUILD_SHARED)
    add_library(lua SHARED ${LUA_SOURCES})
else()
    add_library(lua STATIC ${LUA_SOURCES})
endif()
target_include_directories(lua
    PUBLIC
        $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/src>
        $<INSTALL_INTERFACE:include>
)
target_compile_features(lua PUBLIC c_std_99)
if (WIN32 AND LUA_BUILD_SHARED)
    target_compile_definitions(lua PRIVATE LUA_BUILD_AS_DLL)
endif()
add_library(lua::lua ALIAS lua)
